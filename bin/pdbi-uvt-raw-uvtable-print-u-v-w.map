define uvtable uvt PDBIUVT_INPUT_FILE read
!say 'size(uvt)' ! total array size = NVISI*(3*NSTOKES*NCHAN+7)
define integer iu
define integer iv
define integer iw
define integer ire
define integer iim
define integer iwt
say " "
say "UVT%NCHAN = "'UVT%NCHAN'
say "UVT%NSTOKES = "'UVT%NSTOKES'
say " "
sic output PDBIUVT_OUTPUT_FILE new
for i 1 to UVT%NVISI
    let iu = 1 ! first 7 columns are u,v,w,date,date,n1,n2, then followed by visibilities re im wt
    let iv = 2 ! first 7 columns are u,v,w,date,date,n1,n2, then followed by visibilities re im wt
    let iw = 3 ! first 7 columns are u,v,w,date,date,n1,n2, then followed by visibilities re im wt
    ! if the uv table has only one channel and one stokes, then we also output re im wt amp
    if (UVT%NCHAN.EQ.1).AND.(UVT%NSTOKES.EQ.1) then
        let ire = 7+(1-1)*(UVT%NSTOKES)+1
        let iim = 7+(1-1)*(UVT%NSTOKES)+1+1
        let iwt = 7+(1-1)*(UVT%NSTOKES)+1+2
        if i.EQ.1 then
            say "#" "ivis" " " "u" " " "v" " " "w" " " "re" " " "im" " " "wt" " " "amp" /format A1 A11 A1 A14 A1 A14 A1 A14 A1 A14 A1 A14 A1 A14 A1 A14
        endif
        say 'i' " " 'uvt[iu,i]' " " 'uvt[iv,i]' " " 'uvt[iw,i]' " " 'uvt[ire,i]' " " 'uvt[iim,i]' " " 'uvt[iwt,i]' " " 'sqrt(uvt[ire,i]*uvt[ire,i]+uvt[iim,i]*uvt[iim,i])' /format I12 A1 F14.3 A1 F14.3 A1 F14.3 A1 E14.3 A1 E14.3 A1 E14.3 A1 E14.3
    else
        if i.EQ.1 then
            say "#" "ivis" " " "u" " " "v" " " "w" /format A1 A11 A1 A14 A1 A14 A1 A14
        endif
        say 'i' " " 'uvt[iu,i]' " " 'uvt[iv,i]' " " 'uvt[iw,i]' /format I12 A1 F14.3 A1 F14.3 A1 F14.3
    endif
  !if i.GT.2 break !debug
next
sic output
delete /variable uvt
delete /variable iu iv iw ire iim iwt