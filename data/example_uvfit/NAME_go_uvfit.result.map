! GIDLAS mapping script

let SIC%TEE .TRUE.

let name NAME_go_uvfit
define image f 'name'.uvfit read /global

define integer nlength ilength
nlength = f%dim[2]-4
if (mod(nlength,15).eq.0) then
  ilength = 15 ! Old uvfit format
else if (mod(nlength,17).eq.0) then
  ilength = 17 ! New uvfit format
endif

define integer nchan
let nchan f%dim[1]
say nchan = 'nchan'

define integer nobj
let nobj nlength/ilength

define real rf_x rf_x_err rf_y rf_y_err rf_freq rf_flux rf_flux_err rf_major rf_major_err rf_minor rf_minor_err rf_PA rf_PA_err rf_snr
for iobj 1 to nobj
  sic output NAME_go_uvfit.result.obj_'iobj'.txt new
  say # "x" "x_err" "y" "y_err" "freq" "flux" "flux_err" "major" "major_err" "minor" "minor_err" "PA" "PA_err" /format A1 A9 A10 A10 A10 A15 A19 A19 A14 A14 A14 A14 A14 A14 A14 A14
  say # arcsec arcsec arcsec arcsec GHz mJy mJy arcsec arcsec arcsec arcsec degree degree /format A1 A9 A10 A10 A10 A15 A19 A19 A14 A14 A14 A14 A14 A14 A14 A14
  for ichan 1 to nchan
    let rf_freq    -(f[ichan,4]-f%veloff)*f%restfre/299792.458+f%restfre
    let rf_x         f[ichan,'08+(iobj-1)*ilength']
    let rf_x_err     f[ichan,'09+(iobj-1)*ilength']
    let rf_y         f[ichan,'10+(iobj-1)*ilength']
    let rf_y_err     f[ichan,'11+(iobj-1)*ilength']
    let rf_flux      f[ichan,'12+(iobj-1)*ilength']
    let rf_flux_err  f[ichan,'13+(iobj-1)*ilength']
    let rf_major     f[ichan,'14+(iobj-1)*ilength']
    let rf_major_err f[ichan,'15+(iobj-1)*ilength']
    let rf_minor     f[ichan,'16+(iobj-1)*ilength']
    let rf_minor_err f[ichan,'17+(iobj-1)*ilength']
    let rf_PA        f[ichan,'18+(iobj-1)*ilength']
    let rf_PA_err    f[ichan,'19+(iobj-1)*ilength']
    let rf_freq      rf_freq/1e3        ! to GHz
    let rf_flux      rf_flux*1e3        ! to mJy
    let rf_flux_err  rf_flux_err*1e3    ! to mJy
    let rf_snr       rf_flux/rf_flux_err
    if (rf_snr.LT.0) let rf_snr 0
    say rf_x rf_x_err rf_y rf_y_err rf_freq rf_flux rf_flux_err rf_major rf_major_err rf_minor rf_minor_err rf_PA rf_PA_err /format F10.3 F10.3 F10.3 F10.3 F15.3 E19.7 E19.7 F14.3 F14.3 F14.3 F14.3 F14.3 F14.3
  next ichan
  say ""
  sic output
next iobj

!go plotfit
!hardcopy NAME_go_uvfit.plotfit.'iorder'.eps /overwrite

